/*This project will demonstrate the basic cleaning data for Indonesian Family Life Survey (IFLS)
IFLS is a large longitudinal survey conducted from 1993 to 2014 which consists about 80,000+ individuals and 15,000+ households 
It contains socioeconomic and sociodemographic information such as education, assets, income, consumption, health, intergenerational relationships, and many more
You can get the data and other documents for free from https://www.rand.org/
IFLS uses a "code" to name the variable, if you are new to IFLS i suggest to read the user guide and quesioner book for better understanding*/

/*The topic for this project is to estimate the determination of owning a private health insurance in Indonesia,
i will use cross section data from IFLS 2014 (wave 5) for the datasets. Probit regression will be used to estimate the probability
of individuals to have a private health insurance*/


/*================================================================
						    CLEANING DATA 2014 (Wave 5)
==================================================================*/

/*Im using a working directory for the data so it easier to open and save the data
I usually start with creating the roster data that consist member of the household*/

//Book K Data - AR Section (Control Book and Household Roster)
  use "$hh14\bk_ar1.dta", clear
  count
//Create a roster dataset (Household member)
  duplicates report pidlink
  duplicates report hhid14 pid14
  bys pidlink: gen dup_pidlink=_N
  gen member14_ar=(ar01a==1 | ar01a==2 |ar01a==5 | ar01a==11)
//Create household size variabel
  bys hhid14: egen hhsize=sum(member14_ar)
  tab ar01a member14_ar
  keep if member14_ar==1
    save "$output\roster14", replace

//Book K Data - AR Section (Control Book and Household Roster)
  use "$hh14\bk_ar1.dta", clear
//Create sex variable
  gen male=ar07
  replace male=0 if ar07==3
  label define ar07 0"0:female" 1"1:male", add modify
  label values male ar07
  tab male

//Create age and age square variables
  tab ar09,m
  gen age=ar09
**The number of age that are doesn't makes sense will be recorde to missing (.) in this case there are age that coded to 999 and 998
  replace age =. if inlist(age,999,998)
  tab age
  gen agesq = age^2

//Create a marital status variable
  tab ar13
  gen married = 1 if ar13==2
  replace married = 0  if inlist(ar13,1,3,4,5,6)
  replace married = . if inlist(ar13,8,9)
  label define married 1"1:Married" 0"0:Unmarried (Haven't/divorce)", add modify
  label values married married
  tab married, m

//keep the variables
  keep hhid14 pid14 pidlink age agesq male married
  save "$output\ar14", replace

//Book K Data - SC Section (Sampling and enumeration record)
  use "$hh14\bk_sc1", clear
  tab sc05,m
//Create a residential area variable
  gen urban=1 if sc05==1
  replace urban=0 if sc05==2
  label define urban 1"1:Urban" 0"0:Rural", add modify
  label value urban urban
  tab urban, m
  keep hhid14 urban
  save "$output\residentialarea14", replace  

//Book 3A Data - DL Section (Education)
  use "$hh14/b3a_dl1", clear
  tab dl06
 **Credit: This part of coding is created by Firman Witoelar 
//Create educational variables (Years of Schooling)
  gen education=0       if dl06==1 | dl06==90 | dl06==. | dl07==. | (dl06==98 & dl07==98)
  replace  education=dl07    if (dl06==2 | dl06==11 | dl06==14 | dl06==72) & dl07<7
  replace  education=6       if (dl06==2 | dl06==11 | dl06==14 | dl06==72) & dl07==7 ///
                              | (dl06==95 & dl07==7) | dl06==98 & dl07==7
  replace  education=5       if (dl06==2 | dl06==11 | dl06==14 | dl06==72) & dl07>7
  replace  education=6+dl07  if (dl06==3 | dl06==4 | dl06==12 | dl06==17 | dl06==73) ///
                               & dl07<4
  replace  education=9       if (dl06==3 | dl06==4 | dl06==12 | dl06==17 | dl06==73) /// 
                               & (dl07==4 | dl07==5 | dl07==7)
  replace  education=8       if (dl06==3 | dl06==4 | dl06==12 | dl06==17 | dl06==73) /// 
                               & dl07>7
  replace  education=9+dl07  if (dl06==5 | dl06==6 | dl06==15 | dl06==74) & dl07<4
  replace  education=12      if (dl06==5 | dl06==6 | dl06==15 | dl06==74) & ///
                              (dl07==4|dl07==7)
  replace  education=11      if (dl06==5 | dl06==6 | dl06==15 | dl06==74) & dl07==98
  replace  education=12+dl07 if (dl06==13 | dl06==60 | dl06==61 | dl06==62 | dl06==63) /// 
                               & dl07<5
  replace  education=16      if (dl06==13 | dl06==60 | dl06==61 | dl06==62 | dl06==63) /// 
                               & (dl07==5 | dl07==6 | dl07==7)
  replace education=15       if (dl06==13 | dl06==60 | dl06==61 | dl06==62 | dl06==63)& dl07>7
  replace education=0 if education==.
  tab education

//Categorical Educational Variable
  gen     cat_educ=1 if education==0 
  replace cat_educ=2 if education>=1 & education<7
  replace ccat_educ=3 if education>=7 & education<=12
  replace cat_educ=4 if education>12

  label define cat_educ 1"1:Didn't go to school" 2"2:Elementary" 3"3:High School" 4"4:University/College"
  label values cat_educ cat_educ
  tab cat_educ

  keep hhid14 pid14 pidlink education cat_educ
  save "$output\education14", replace

//Book 3A Data - TK Section (Employment history)
  use "$hh14\b3a_tk1", clear
**I have to merge with tk2 data for get the tk24a quastion  
  merge 1:1 hhid14 pid14 using "$hh14\b3a_tk2"
  drop _m
  tab tk01
//Create a work status variable
**In this variable, i use tk01 "Most activities during past weeks"
  gen work=tk01==1 if tk01!=.
  label define work 0"0:Doesn't work" 1"1:Work", add modify
  label values work work
  tab work,m

//You can use another type of employment history like employment types
  gen worktype = 1 if inlist(tk01,2,3,4,5,7,95)
  replace worktype = 2 if tk24a==6
  replace worktype = 3 if inlist(tk24a,1,2,3)
  replace worktype = 4 if inlist(tk24a,7,8)
  replace worktype = 5 if inlist(tk24a,4,5)

  label define worktype 1"1:Doesn't work" 2"2:Unpaid family worker" 3"3:Self employed" 4"4:Casual worker" 5"5:Government/private worker"
  label values worktype worktype
  label variable worktype "Employment types"
  tab worktype,m

  keep hhid14 pid14 pidlink work worktype
  save "$output/employment14", replace

//Book 3B Data - KK Section (Health Condition)
  use "$hh14\b3b_kk1", clear
  tab kk01
//Create a health condition (subjective) variable
**This variable is subjective health condition, you can use other variables for other health characteristics
  gen health = 1 if kk01==4
  replace health = 2 if kk01==3
  replace health = 3 if kk01==2
  replace health = 4 if kk01==1

  label define health 1"1:Unhealthy" 2"2:Somewhat unhealthy" 3"3:Somewhat healthy" 4"4:Very healthy", add modify
  label values health health
  tab health
  keep hhid14 pid14 pidlink health
  save "$output\health14", replace

/Book 3B - CD Section (Chronic Condition)
  use "$hh14/b3b_cd2", clear
  tab cd01
  recode cd01 (3=0) (8=.)
  drop if cd01==.
//Create a chronic disease variable 
  keep hhid14 pid14 pidlink cd01 cd01type
  reshape wide cd01, i(pidlink) j(cd01type) string
//I use chronic1 for the first type of chronical condition such as brain damage, disabilities, mental retardation, autism, sense proble, etc.
  gen chronic1= (cd01A==1 | cd01B==1 | cd01C==1 | cd01D==1 | cd01E==1 | cd01F==1 | cd01I==1)
  tab chronic1

  keep hhid14 pid14 chronic1 pidlink
  save "$proses\chronic1_14", replace

  use "$hh14/b3b_cd3", clear
  keep pid14 hhid14 pidlink cd05 cdtype
  tab cd05, m

//Create a chronic disease variable 
  recode cd05 (3=0) (8 9=.)
  drop if cd05==.
  reshape wide cd05, i(pidlink) j(cdtype) string
//I use chronic2 for the second type of chronical condition such as Hypertension, TBC, cancer, diabetes, stroke, etc.
  gen chronic2 = (cd05A==1 | cd05B==1 | cd05C==1 | cd05D==1 |cd05E==1 | cd05F==1 | cd05G==1 | cd05H==1 | cd05I==1 | cd05J==1 | cd05M==1 | cd05N==1 | cd05O==1 | cd05P==1 | cd05Q==1 | cd05R==1)
  tab chronic2
**The variable will be dummy (1 if have chronic disease and 0 if non)

  keep pid14 hhid14 pidlink chronic2
  save "$proses\chronic2_14", replace

//Merging the chronic1 and chronic2 datasets
  use "$proses\chronic1_14", clear
  merge 1:1 hhid14 pid14 using "$proses\chronic2_14"
  drop _m

  gen chronic = chronic1==1 | chronic2==1
  tab chronic
  label define chronic 0"0: No" 1"1: Yes", add modify
  label values chronic chronic
  tab chronic
  save "$output\chronic14", replace

//Book 3B data - AK Section (Health Insurance)
  use "$hh14/b3b_ak1", clear
  tab ak01, m
/*There are many types of health insurance in IFLS, such as Jamkesmas, private health insurance, pt askes, etc.
i will keep the E type because it's the code for private health insurance*/

  keep if aktype == "E" | aktype == ""
//Create a Ownerhip of Private Health Insurance Variable
  gen insurance = 0 if ak01==3
  replace insurance = 1 if ak02==1
  replace insurance = 0 if ak02==3
  replace insurance = . if inlist(ak02,8,9)
  label define insurance 0"0:Don't have" 1"1:Have", add modify
  label values insurance insurance
  tab insurance

  keep pid14 hhid14 pidlink insurance
  save "$output\insurance14", replace

//In this part, i will merge all variables into one dataset

  use "$output\roster14", clear
  
**merge 1:1 means that i combine 2 datasets with the same level, in this case is merging individual level datasets to individual level datasets

  merge 1:1 hhid14 pid14 using "$output\ar14"
  keep if _merge==3
  drop _merge

  merge 1:1 hhid14 pid14 using "$output\education14"
  keep if _merge==3
  drop _merge

  merge 1:1 hhid14 pid14 using "$output\employment14"
  keep if _merge==3
  drop _merge

  merge 1:1 hhid14 pid14 using "$output\health14"
  keep if _merge==3
  drop _merge

  merge 1:1 hhid14 pid14 using "$output\insurance14"
  keep if _merge==3
  drop _merge

  merge 1:1 hhid14 pid14 using "$output\chronic14"
  keep if _merge==3
  drop _merge

**merge m:1 means that i combine 2 datasets with the different level, in this case is merging individual level datasets to household level datasets

  merge m:1 hhid14 using "$output\residentialarea14"
  keep if _merge==3
  drop _merge

//I already have percapita expenditure data (pce14) before
  merge m:1 hhid14 using "$pce\pce14nom"
  keep if _merge==3
  drop _merge

  keep hhid14 pid14 pidlink hhsize male age agesq married education cat_educ work worktype insurance chronic urban pce lpce health

  save "$output\data2014", replace

//This is the final datasets that contain all the variables above
  use "$output\data2014", clear

//We can see the summary statistics of the data with tabulate or summary command
  tab education
  tab (variable)

  sum insurance hhsize male age agesq married education work chronic urban lpce health

/*Variable used :
Dependent : insurance
Independent : hhsize, male, age, agesq, married, education, work, chronic, urban, pce (log), health

//PROBIT ESTIMATION
  probit insurance hhsize male age agesq married education work chronic urban lpce health

//Marginal effect for interpretation, there are 2 options available
  mfx
**or
  margins, dydx(*)










  
